version = '0.17.0'
group 'org.comroid'

apply plugin: 'java-library'
apply from: "$parent.projectDir/gradle/vars.gradle"
apply from: "$parent.projectDir/gradle/javadoc.gradle"
apply from: "$parent.projectDir/gradle/global.gradle"

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    projectDescription = "Java Uniform Adapters"
}

configurations {
    main
    java8compat {
        extendsFrom main
    }
    java11compat {
        extendsFrom main
    }
}

sourceSets {
    java8compat.java.srcDirs = files("$parent.projectDir/src/$artifactName/main/java8compat")
    java11compat.java.srcDirs = files("$parent.projectDir/src/$artifactName/main/java11compat")
}

compileJava8compatJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

compileJava11compatJava {
    sourceCompatibility = 11
    targetCompatibility = 11
}

jar {
    from files(sourceSets.main.output.classesDirs,
            sourceSets.java8compat.output.classesDirs,
            sourceSets.java11compat.output.classesDirs)

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

dependencies {
    java8compatCompileOnly 'org.jetbrains:annotations:19.0.0'
    java11compatCompileOnly 'org.jetbrains:annotations:19.0.0'

    if (isAutoDeploy) {
        api project(':core')

        java8compatCompileOnly project(':restless')
        java11compatCompileOnly project(':restless')

        api project(':uniform')
        java8compatImplementation project(':uniform')
        java11compatImplementation project(':uniform')
        api project(':varbind')
        java8compatImplementation project(':varbind')
        java11compatImplementation project(':varbind')
    } else {
        api project(':guardian:core')

        java8compatCompileOnly project(':guardian:restless')
        java11compatCompileOnly project(':guardian:restless')

        api project(':guardian:uniform')
        java8compatImplementation project(':guardian:uniform')
        java11compatImplementation project(':guardian:uniform')
        api project(':guardian:varbind')
        java8compatImplementation project(':guardian:varbind')
        java11compatImplementation project(':guardian:varbind')
    }

    // http adapter dependencies
    java8compatCompileOnly 'com.squareup.okhttp3:okhttp:4.7.2'

    // logging dependencies
    implementation 'org.apache.logging.log4j:log4j-api:2.11.0'
    java8compatImplementation 'org.apache.logging.log4j:log4j-api:2.11.0'
    java11compatImplementation 'org.apache.logging.log4j:log4j-api:2.11.0'

    testImplementation sourceSets.java8compat.output.classesDirs
    testImplementation sourceSets.java11compat.output.classesDirs
    testRuntimeOnly 'com.squareup.okhttp3:okhttp:4.5.0'
    testImplementation 'com.alibaba:fastjson:1.2.71'
}

apply from: "$parent.projectDir/gradle/publishing.gradle"

